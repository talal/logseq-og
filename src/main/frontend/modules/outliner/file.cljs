(ns frontend.modules.outliner.file
  (:require [cljs-time.coerce :as tc]
            [cljs-time.core :as t]
            [clojure.core.async :as async]
            [clojure.string :as string]
            [frontend.db :as db]
            [frontend.db.model :as model]
            [frontend.handler.notification :as notification]
            [frontend.modules.file.core :as file]
            [frontend.modules.outliner.tree :as tree]
            [frontend.state :as state]
            [frontend.util :as util]
            [goog.object :as gobj]
            [lambdaisland.glogi :as log]))

(def batch-write-interval 1000)

(defn do-write-file!
  [repo page-db-id outliner-op]
  (let [page-block (db/pull repo '[*] page-db-id)
        page-db-id (:db/id page-block)
        blocks-count (model/get-page-blocks-count repo page-db-id)
        blocks-just-deleted? (and (zero? blocks-count)
                                  (contains? #{:delete-blocks :move-blocks} outliner-op))]
    (when (or (>= blocks-count 1) blocks-just-deleted?)
      (if (and (> blocks-count 500)
               (not (state/input-idle? repo {:diff 3000}))) ;; long page
        (async/put! (state/get-file-write-chan) [repo page-db-id outliner-op (tc/to-long (t/now))])
        (let [blocks (model/get-page-blocks-no-cache repo (:block/name page-block) {:pull-keys '[*]})]
          (when-not (and (= 1 (count blocks))
                         (string/blank? (:block/content (first blocks)))
                         (nil? (:block/file page-block)))
            (let [tree-or-blocks (tree/blocks->vec-tree repo blocks (:block/name page-block))]
              (if page-block
                (file/save-tree! page-block tree-or-blocks blocks-just-deleted?)
                (js/console.error (str "can't find page id: " page-db-id))))))))))

(defn write-files!
  [pages]
  (when (seq pages)
    (doseq [[repo page-id outliner-op] (set (map #(take 3 %) pages))] ; remove time to dedupe pages to write
      (try (do-write-file! repo page-id outliner-op)
           (catch :default e
             (notification/show!
              [:div
               [:p "Write file failed, please copy the changes to other editors in case of losing data."]
               "Error: " (str (gobj/get e "stack"))]
              :error)
             (log/error :file/write-file-error {:error e}))))))

(defn sync-to-file
  ([page]
   (sync-to-file page nil))
  ([{page-db-id :db/id} outliner-op]
   (if (nil? page-db-id)
     (notification/show!
      "Write file failed, can't find the current page!"
      :error)
     (let [repo (state/get-current-repo)]
       (if (:graph/importing @state/state) ; write immediately
         (write-files! [[repo page-db-id outliner-op]])
         (async/put! (state/get-file-write-chan) [repo page-db-id outliner-op (tc/to-long (t/now))]))))))

(def *writes-finished? (atom {}))

(defn <ratelimit-file-writes!
  []
  (util/<ratelimit (state/get-file-write-chan) batch-write-interval
                   :filter-fn
                   (fn [[repo _ _ time]]
                     (swap! *writes-finished? assoc repo {:time time
                                                          :value false})
                     true)
                   :flush-fn
                   (fn [col]
                     (let [start-time (tc/to-long (t/now))
                           repos (distinct (map first col))]
                       (write-files! col)
                       (doseq [repo repos]
                         (let [last-write-time (get-in @*writes-finished? [repo :time])]
                           (when (> start-time last-write-time)
                             (swap! *writes-finished? assoc repo {:value true}))))))))
